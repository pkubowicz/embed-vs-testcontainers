plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72' apply false
    id 'org.jetbrains.kotlin.plugin.spring' version '1.3.72' apply false
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'org.springframework.boot' version '2.2.8.RELEASE' apply false
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'kotlin'
    apply plugin: 'org.jetbrains.kotlin.plugin.allopen'

    repositories {
        jcenter()
    }

    dependencies {
        implementation('org.jetbrains.kotlin:kotlin-stdlib-jdk8')
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage'
        }
        testImplementation("org.springframework.security:spring-security-test")
    }

    test {
        useJUnitPlatform()
        systemProperty 'spring.test.constructor.autowire.mode', 'all'
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = '11'
        }
    }

    allOpen {
        annotation("org.springframework.web.bind.annotation.RestController")
        annotation("org.springframework.context.annotation.Configuration")
    }
}
